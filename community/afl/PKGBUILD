# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Santiago Torres-Arias <santiago[at]archlinux[dot]org>
# Contributor: spider-mario <spidermario@free.fr>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - make with AFL_NO_X86=1
#  - remove makedepend on lib32-glibc

pkgname=afl
pkgver=2.57b
pkgrel=1
pkgdesc='Security-oriented fuzzer using compile-time instrumentation and genetic algorithms'
url='http://lcamtuf.coredump.cx/afl/'
arch=('x86_64')
license=('Apache')
depends=('glibc' 'bash')
optdepends=('gcc: gcc instrumentation support'
            'clang: clang instrumentation support'
            'llvm: experimental clang-fast instrumentation support'
            'gnuplot: graph plotting support')
makedepends=('clang' 'llvm' 'gcc' 'wget' 'python')
provides=('american-fuzzy-lop')
replaces=('american-fuzzy-lop')
options=('!emptydirs' '!strip')
source=(https://github.com/google/AFL/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('a6416350ef40e042085337b0cbc12ac95d312f124d11f37076672f511fe31608')
sha512sums=('78e2191b6be93b0b50a347b019422ee43013be8ae567e84e54059788e11cc66c67d89847e4580da55b8f6449efdf99d8efca5bce08d439fbb4a4a3311b942eb9')
b2sums=('08610165db4438da55aad47935fd392d2992f7abae0ddba182e0637157ab69cb70a79e0dd53e1cb9f5bed4ced8af9a285cc0e4ee22c3b8fb5a83db5e82ae15b8')

build() {
  cd AFL-${pkgver}
  make PREFIX=/usr AFL_NO_X86=1
  make -C llvm_mode PREFIX=/usr
}

package() {
  cd AFL-${pkgver}
  make PREFIX=/usr AFL_NO_X86=1 DESTDIR="${pkgdir}" install
  install -Dm 644 llvm_mode/README.llvm -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
